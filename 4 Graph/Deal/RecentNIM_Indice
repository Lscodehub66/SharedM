// let LoadCode = Load("RecentNIM_Indice","Code"), EVA = Expression.Evaluate(LoadCode, #shared)
let
    _VolCol = "V_NIM_1D",
    _CountCol = "CT_NIM_1D",
    Col = {_VolCol, _CountCol},
    ExpDateCol = {"A_Date", "每周第一天"},
    RecentDimDate =
        let
            datetbl = fx("DateFromRecord")(fx("DateRange")({"StartOfRecent.Date", "当前"}), {"A_Date", "每周第一天"}),
            Renamed = Table.RenameColumns(datetbl, {{"A_Date", "Date"}}, MissingField.Ignore),
            InnerMerged = Table.NestedJoin(Renamed, {"Date"}, CWB("mIndice"), {"Date"}, "Data", JoinKind.Inner),
            #"Removed Columns" = Table.RemoveColumns(InnerMerged, {"Data"})
        in
            #"Removed Columns",
    Source = fx("DBMonitor")("Recent"),
    #"Inserted Day of Week" = Table.AddColumn(
        Source,
        "Date",
        each
            if Date.DayOfWeek([定价日], Day.Monday) = 5 then
                Date.AddDays([定价日], -1)
            else if Date.DayOfWeek([定价日], Day.Monday) = 6 then
                Date.AddDays([定价日], -2)
            else
                [定价日],
        type date
    ),
    #">>GroupedByDay" = Table.Group(
        #"Inserted Day of Week",
        {"Date"},
        {{_CountCol, each Table.RowCount(_), Int64.Type}, {_VolCol, each List.Sum([亿美元]), type number}}
    ),
    #"Merged Queries" = fx("Merge")(RecentDimDate, #">>GroupedByDay", "Date", "Date", {""}, {""}, {"Keep"}),
    #"Replaced Value" = Table.ReplaceValue(#"Merged Queries", null, 0, Replacer.ReplaceValue, Col),
    #"Grouped Rows" = Table.Group(#"Replaced Value", {"每周第一天"}, {{"Custom", each _, type table}}),
    Custom1 = List.Accumulate(
        List.Zip({Col, List.Transform(Col, each Text.Replace(_, "1D", "7D"))}),
        #"Grouped Rows",
        (x, y) => Table.AddColumn(x, y{1}, each List.Sum(Table.Column([Custom], y{0})), type number)
    ),
    Custom2 = fx("Exp")(Custom1, "Custom"),
    AddedKey = Table.AddKey(Custom2, {"Date"},true),
    #"Sorted Rows" = Table.Sort(    AddedKey, {{"Date", Order.Descending}})
in
    #"Sorted Rows"
