// let LoadCode = Load("Spd_Avg","Code"), EVA = Expression.Evaluate(LoadCode, #shared) in EVA

let

    //
    Source =CWB("mIndice"),  
    //
     CN_Source = List.Buffer(Table.ColumnNames(Source)),
    MondayDate =if OVRDSpd_Monday = "" or OVRDSpd_Monday = null  then Load("Controller")[Monday]{0} else OVRDSpd_Monday,
     _InputCurves = {"UST", "CGB"},    // second item minus by first item
        CurveHD = List.Select(
        CN_Source,
        each Text.Contains(_, _InputCurves{0}) and Text.Contains(_, Number.ToText(Load("Spd_Tenor"), "0Y"))
    )
        & List.Select(
            CN_Source,
            each Text.Contains(_, _InputCurves{1}) and Text.Contains(_, Number.ToText(Load("Spd_Tenor"), "0Y"))
        ),
    //
    Filtered = Table.SelectRows(   Source, each [Date] <    MondayDate),
   
    Sorted = Table.Sort(Filtered, {{"Date", Order.Descending}}),
    SCol = Table.SelectColumns(Sorted, {"Date"} & CurveHD),
    //
    #"Inserted Substraction" = Table.AddColumn(
        SCol,
        "Spd",
        each Number.Round((Record.Field(_, CurveHD{1}) - Record.Field(_, CurveHD{0})) * 10000, 0),
        type number
    ),
    #"Inserted Abs" = Table.AddColumn(
        #"Inserted Substraction", "Spd" & "_Abs", each Number.Abs(Record.Field(_, "Spd")), type number
    ),
    #"Inserted Trend" = Table.AddColumn(
        #"Inserted Abs", "Trend", each if Record.Field(_, "Spd") < 0 then "倒挂" else "正值", type text
    ),
    #"Invoked NextRow" = Load("fxNextRow")(#"Inserted Trend", "Trend", Load("Prefix") & "_NextRow"),
    #"Inserted Compare" = Table.AddColumn(
        #"Invoked NextRow",
        Load("Prefix") & "_转向",
        each
            if
                Record.Field(_, "Trend") <> Record.Field(_, Load("Prefix") & "_NextRow")
                and Record.Field(_, Load("Prefix") & "_NextRow") <> null
            then
                "Y"
            else
                null
    ),
    Replaced = Table.ReplaceValue(
        #"Inserted Compare",
        each Record.Field(_, "Trend"),
        each
            if
                Record.Field(_, "Trend") <> Record.Field(_, Load("Prefix") & "_NextRow")
                and Record.Field(_, Load("Prefix") & "_NextRow") <> null
            then
                "转向"
            else
                Record.Field(_, "Trend"),
        Replacer.ReplaceValue,
        {"Trend"}
    ),
    Avg1_Filtered = Table.SelectRows(
        Table.FillDown(Replaced, {Load("Prefix") & "_转向"}),
        each Record.Field(_, Load("Prefix") & "_转向") = Table.Column(Replaced, Load("Prefix") & "_转向"){0}
    ),
    Avg1_Value = Number.Round(List.Average(Table.Column(Avg1_Filtered, "Spd")), 0),
    Avg2_Filtered = Table.SelectRows(
        Table.FillDown(Replaced, {Load("Prefix") & "_转向"}),
        each Record.Field(_, Load("Prefix") & "_转向") <> Table.Column(Replaced, Load("Prefix") & "_转向"){0}
    ),
    Avg2_Value = Number.Round(List.Average(Table.Column(Avg2_Filtered, "Spd")), 0),
    Avg_Spd = Table.AddColumn(
        Replaced, "Spd" & "_Avg", each if Record.Field(_, "Spd") <= 0 then Avg1_Value else Avg2_Value, Int64.Type
    ),
    RCol = Load("fxRemoveCol")(Avg_Spd)
in
    RCol