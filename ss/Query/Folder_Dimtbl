let
    GPS = try Excel.CurrentWorkbook(){[Name = "GPS"]}[Content][Column1]{0} otherwise "Remote",
    FDSource_CN = {"PK_Folder", "Folder_Remote", "Folder_Global", "Folder_Segment"},
    FDSource = Table.FromRows(
        {
            {"Base*", "\Business\Hub Drive\DCM Base", "\0 DCM Base", ""},
            {"Base_4", null, null, "\4 Markets"},
            {"Base_5_RSP", null, null, "\5 Databases\RSP Database"},
            {"Team*", "\EXE Team", "\Team Folder\Execution Team", ""},
            {"Team_DH", null, null, "\DataHub"},
            {"Team_AM", null, null, "\Weekly AM"},
            {"Pipeline", "", "\Pipeline Report", ""},
            {"QRC", "\EXE Team\QRC", "\【QRC】DCM项目情况表", ""}
        },
        FDSource_CN
    ),
    FDSource_Root = if GPS = "Remote" then "C:\Users\ktkt2\OneDrive" else "\\10.10.1.215\dcm$",
    FDSource_CN_GPS = List.Select({"Folder_Remote", "Folder_Global"}, each Text.Contains(_, GPS)),
    FDFilledDown = Table.FillDown(FDSource, List.Intersect({{"Folder_Remote", "Folder_Global"}, FDSource_CN_GPS})),
    FDOutput = Table.AddColumn(
        FDFilledDown, "Folder_Path", each FDSource_Root & Record.Field(_, FDSource_CN_GPS{0}) & [Folder_Segment]
    ),
    FDFiltered = Table.SelectRows(
        FDOutput, each not Text.Contains([PK_Folder], "*") and Record.Field(_, FDSource_CN_GPS{0}) <> ""
    ),
   FDSCol = Table.SelectColumns(FDFiltered,{"PK_Folder", "Folder_Path"})
    in
     FDSCol