let func =  
 (String as text, optional Delimiter as text) as text =>
let
    delimiter = if Delimiter = null then " " else Delimiter,
    TextToList = List.Buffer(Text.Split(String, delimiter)),
    FilterList = List.Select(TextToList, each _ <> ""),
    Result = Text.Combine(FilterList, delimiter)
in
    Result
, documentation = [
Documentation.Name =  " Text.RemoveRepeatingCharacters
", Documentation.Description = " Removes repeating characters of the delimiter from a <code>String</code>
" , Documentation.LongDescription = " Removes repeating characters of the delimiter from a <code>String</code>. <code>Delimiter</code> is by default set to space/blank.
", Documentation.Category = " Text
", Documentation.Source = " http://wp.me/p6lgsG-M9  .
", Documentation.Author = " Imke Feldmann: www.TheBIccountant.com .
", Documentation.Examples = {[Description =  " Removes repeating characters of the delimiter from a string: https://wp.me/p6lgsG-M9 .
" , Code = " RemoveRepeatingCharacters(""Imke      Feldmann"")
 ", Result = " ""Imke Feldmann""
"]}] 
 in 
  Value.ReplaceType(func, Value.ReplaceMetadata(Value.Type(func), documentation))