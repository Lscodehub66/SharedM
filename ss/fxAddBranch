//Example
//Table.AddColumn(SelectedCol, "通过分行", each if Text.Contains([信用主体],"银行") and fxSNBranch([发行人]) = "" then "【总行】" else if Text.Contains([信用主体],"银行") then  fxSNBranch([发行人]) & "分行" else "")
//Function
(TextName as text) =>
    let
        PrepText = fxCleaner(Text.Upper(TextName), null, {"MACAO", " ", ",", ".", "CHINA", "NY", "THE"}, {"MACAU"}),
        Text = if Text.Contains(PrepText, "银行") or Text.Contains(PrepText, "BANK") then PrepText else "",
        ExtractedCN =
            if Text = "" then
                ""
            else if List.Contains({"A".."Z"}, Text.Start(Text, 1)) then
                Text
            else if Text.Contains(Text, "/") then
                Text.AfterDelimiter(Text, "/")
            else if Text.Contains(Text, "分行") and Text.Contains(Text, "公司") then
                Text.BetweenDelimiters(Text, "公司", "分行")
            else if Text.Contains(Text, "分行") and Text.Contains(Text, "银行") then
                Text.BetweenDelimiters(Text, "银行", "分行")
            else
                "【总行】",
        ExtractedEN =
            if
                List.Contains({"A".."Z"}, Text.Start(ExtractedCN, 1))
                and (Text.Contains(ExtractedCN, "/") or Text.Contains(ExtractedCN, "BRANCH"))
            then
                fxCleaner(
                    Text.BetweenDelimiters(
                        fxCleaner(ExtractedCN, "Location^"), "^", "^", {1, RelativePosition.FromEnd}, 0
                    ),
                    "LocationCN"
                )
            else if List.Contains({"A".."Z"}, Text.Start(ExtractedCN, 1)) then
                "【总行】"
            else
                ExtractedCN,
        AddedSuffix =
            if ExtractedEN <> "" and not Text.Contains(ExtractedEN, "【") then
                ExtractedEN & "分行"
            else
                ExtractedEN
    in
        AddedSuffix
