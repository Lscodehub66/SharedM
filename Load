//let Name = "Load",Path  = if (try Excel.CurrentWorkbook(){[ Name = "GPS" ]}[Content][Column1]{0} otherwise "Remote" ) = "Remote" then "C:\Users\ktkt2\OneDrive\EXE Team\DataHub\SharedM\" else "\\itd-m09-fs02\dcm$\Team Folder\Execution Team\DataHub\SharedM\",  EVA = Expression.Evaluate ( Text.FromBinary ( Binary.Buffer ( File.Contents ( Path&Name ) ) ) ,#shared )in    EVA////
(NameInput as text, optional PathInput as text, optional ColList as list) =>
    let
        //NameInput = "", PathInput = null,
        GPS = try Table.Buffer(Excel.CurrentWorkbook(){[Name = "GPS"]}[Content])[Column1]{0} otherwise "Remote",
        Name_File =
            if NameInput = "Control_Date" then
                "Control"
            else if PathInput = "CSV" then
                NameInput & ".csv"
            else
                Text.Replace(NameInput, ".", "_"),
        //
        Folder_Root =
            if GPS = "Remote" then
                "C:\Users\ktkt2\OneDrive\EXE Team\DataHub\"
            else
                "\\itd-m09-fs02\dcm$\Team Folder\Execution Team\DataHub\",
        Path_Folder =
            if not List.Contains({null, "1", "Code", "CSV"}, PathInput) then
                PathInput
            else
                Folder_Root & "SharedM",
        Path = Path_Folder & (if Text.End(Path_Folder, 1) <> "\" then "\" else ""),
        Path_GitHub = "https://raw.githubusercontent.com/Lscodehub66/DataHub/main/",
        //
        File_Folder = Folder.Files(Path),
        File_Filtered = Table.SelectRows(File_Folder, each [Name] = Name_File),
        File_Content = File_Filtered{0}[Content],
        //"Code" "CSV" options here
        File_Code = Text.FromBinary(Binary.Buffer(File_Content)),
        File_CSV = Table.PromoteHeaders(Csv.Document(File_Content), [PromoteAllScalars = true]),
        AddedKey = if NameInput = "DimFX" then Table.AddKey(File_CSV, {"YYQQ", "Base Currency"}, true) else File_CSV,
        //
        EVA_Existing = Expression.Evaluate(Name_File, #shared),
        EVA_Code = Expression.Evaluate(File_Code, #shared),
        EVA = try EVA_Existing otherwise EVA_Code,
        //
        Output = if PathInput = "Code" or PathInput = "1" then File_Code else if PathInput = "CSV" then AddedKey else EVA,
        SCol_Output =
            if ColList = null or ColList = {null} then
                Output
            else
                Table.SelectColumns(Output, ColList, MissingField.UseNull)
    in
        SCol_Output
