let
    CWBName = "mIndice",
    _CurrentDate = List.Buffer(Load("Status")[Monday]){0},
    Source = Table.SelectRows(try CWB(CWBName) otherwise Load(CWBName), each [Date] < _CurrentDate),
    Sorted = Table.Sort(Source, {{"Date", Order.Descending}}),
    _InputCol = {"R_UST_3Y", "R_CGB_3Y"},
    // second item minus by first item
    _DAX = "Spd",
    _TrendCol = "Trend",
    _CalculatedDES = List.Transform(_InputCol, each Text.AfterDelimiter(_, "_")){1}
        & "-"
        & List.Transform(_InputCol, each Text.AfterDelimiter(_, "_")){0},
    ACol0 = Table.AddColumn(Sorted, "计算", each _CalculatedDES, type text),
    ACol = Table.AddColumn(
        ACol0,
        _DAX,
        each Number.Round((Record.Field(_, _InputCol{1}) - Record.Field(_, _InputCol{0})) * 10000, 0),
        type number
    ),
    ACol2 = Table.AddColumn(ACol, _TrendCol, each if Record.Field(_, _DAX) < 0 then "倒挂" else "正常", type text),
    SCol = Table.SelectColumns(ACol2, {"计算", "Date", _DAX, _TrendCol}),
    Custom1 = Load("fxNextRow")(SCol, _TrendCol),
    NextRow = Table.AddColumn(
        Custom1,
        _TrendCol & "_转向",
        each if Record.Field(_, _TrendCol) <> [NextRow] and [NextRow] <> null then "Y" else null
    ),
    #"Filled Down" = Table.FillDown(NextRow, {_TrendCol & "_转向"}),
    RCol1 = Table.RemoveColumns(#"Filled Down", {"NextRow"}),
    Filtered = Table.SelectRows(RCol1, each (Record.Field(_, _TrendCol & "_转向") = null)),
    _AvgValue = List.Average(Table.Column(Filtered, _DAX)),
    _AvgValue_2 = List.Average(
        Table.Column(Table.SelectRows(RCol1, each (Record.Field(_, _TrendCol & "_转向") <> null)), _DAX)
    ),
    Avg_SPD = Table.AddColumn(Table.FirstN(Filtered, 1), _DAX & "_Avg", each _AvgValue, Int64.Type),
    Avg_Date = Table.AddColumn(Avg_SPD, "Date_Min", each List.Min(Table.Column(Filtered, "Date")), type date),
    Avg_SPD_2 = Table.AddColumn(Avg_Date, _DAX & "_Avg" & "_转向前平均", each _AvgValue_2, Int64.Type),
    _PeakValue = if _AvgValue < 0 then List.Min(Table.Column(Filtered, _DAX)) else List.Max(
        Table.Column(Filtered, _DAX)
    ),
    _PeakRow = Table.SelectRows(Filtered, each Record.Field(_, _DAX) = _PeakValue),
    Peak_SPD = Table.AddColumn(Avg_SPD_2, _DAX & "_Peak", each Table.Column(_PeakRow, _DAX){0}, type number),
    Peak_Date = Table.AddColumn(Peak_SPD, "Date_Peak", each Table.Column(_PeakRow, "Date"){0}, type date),
    Peak_IsPeak = Table.AddColumn(
        Peak_Date, _TrendCol & "_Is_Peak", each if [Date] <> [Date_Peak] then "已收窄" else "达到最高值", type text
    ),
    Range = Table.AddColumn(Peak_IsPeak, "Duration", each Duration.Days([Date] - [Date_Min]), type duration),
    Custom2 = fxDataFormat(Range),
    Writer = Table.AddColumn(
        Custom2,
        "描述",
        each
            "当前，中美利差为"
                & Number.ToText(Record.Field(_, _DAX), "0bps")
                & "，"&"相较"
                & Date.ToText([Date_Peak], "yyyy年M月")
                & "的最高点"
                & Number.ToText(Record.Field(_, _DAX & "_Peak"), "0bps")
                & Record.Field(_, _TrendCol & "_Is_Peak")
                & "。"
                & "#(lf)"
                & "自转向以来，中美利差平均约为"
                & Number.ToText(Record.Field(_, _DAX & "_Avg"), "0bps"),
        type text
    )
in
    Writer
