let
    //
    fxConnect = fx("Source"),
    fxConnect_YY = Load("fxConnect_YY"),
    //
    SourceFI = fxConnect("YY_金融"),
    SourceIndustrial = fxConnect("YY_产业"),
    SourceLGFV = fxConnect("YY_城投"),
    Combined = Load("fxDataFormat")(Table.Combine({SourceFI, SourceLGFV, SourceIndustrial})),
    SelectedCol = Table.SelectColumns(Combined, {"主体", "主体类型", "大股东持股", "是否上市公司"}, MissingField.UseNull),
    ABDelimiter = Table.AddColumn(
        SelectedCol,
        "大股东股份占比",
        each
            Text.Select(
                Text.BetweenDelimiters([大股东持股], "（", "）", {0, RelativePosition.FromEnd}, 0), {"0".."9", ".", "%"}
            ),
        type text
    ),
    ABDelimiter1 = Table.AddColumn(
        ABDelimiter,
        "大股东",
        each fxCleaner(Text.BeforeDelimiter([大股东持股], "（", {0, RelativePosition.FromEnd}), "主体"),
        type text
    ),
    ACol = Table.AddColumn(
        ABDelimiter1,
        "大股东政府机构",
        each
            if
                [大股东] = "HKSCC"
                or Text.Contains([大股东], "政府")
                or Text.EndsWith([大股东], "办公室")
                or Text.EndsWith([大股东], "院")
                or Text.EndsWith([大股东], "部")
                or Text.EndsWith([大股东], "厅")
                or Text.EndsWith([大股东], "局")
                or Text.EndsWith([大股东], "委")
                or Text.EndsWith([大股东], "中心")
                or Text.EndsWith([大股东], "会")
            then
                [大股东]
            else
                "",
        type text
    ),
    ACol1 = Table.AddColumn(ACol, "大股东企业", each if [大股东政府机构] = "" then [大股东] else "", type text),
    TF = Table.TransformColumns(ACol1, {{"是否上市公司", each if _ = "否" then "" else _}}),
    Filtered = Table.SelectRows(
        TF,
        each [主体] <> null and [主体] <> "" and not Text.Contains([主体], "数据来源") and ([大股东持股] <> "" or [是否上市公司] <> "")
    ),
    ChangedType = Table.TransformColumnTypes(Filtered, {{"大股东股份占比", type number}}),
    Merged = Load("fxMerge")(ChangedType, CWB("mCredit"), "主体", "信用主体", {"Index", "系"}, {"Index", "系"}),
    Filtered1 = Table.SelectRows(Merged, each [Index] <> null),
    SelectedCol1 = Table.SelectColumns(Filtered1, {"主体", "大股东企业", "系", "大股东政府机构", "大股东股份占比", "是否上市公司"})
in
    SelectedCol1
