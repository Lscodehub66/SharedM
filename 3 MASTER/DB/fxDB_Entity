(TableName as table) =>
    let
        Source = TableName,
        CN_Source = List.Buffer(Table.ColumnNames(Source)),
        // Revalidate mCredit
        Entity_省级 =
            if List.ContainsAll(CN_Source, {"级别", "城市"}) then
                Table.ReplaceValue(
                    Source, each [城市], each if [级别] = "省级" then "" else [城市], Replacer.ReplaceValue, {"城市"}
                )
            else
                Source,
        Entity_市级 =
            if List.ContainsAll(CN_Source, {"级别", "区县"}) then
                Table.ReplaceValue(
                    Entity_省级, each [区县], each if [级别] = "市级" then "" else [区县], Replacer.ReplaceValue, {"区县"}
                )
            else
                Entity_省级,
        Entity_级别 =
            if Table.HasColumns(Source, "级别") then
                Table.ReplaceValue(
                    Entity_市级,
                    "",
                    each if [板块] = "地方平台" then "【】" else if [中资属性] <> "地方国企" then "--" else "",
                    Replacer.ReplaceValue,
                    {"级别"}
                )
            else
                Entity_市级,
        Entity_系 =
            if Table.HasColumns(Source, "系") then
                Table.ReplaceValue(
                    Entity_级别, "", each if [中资属性] = "央企(子)" then "【】" else "", Replacer.ReplaceValue, {"系"}
                )
            else
                Entity_级别,
        Entity_Default = Table.ReplaceValue(
            Entity_系, "", each if [IS_DEFAULT] = "Y*" then "--" else "", Replacer.ReplaceValue, {"地区", "板块", "中资属性"}
        ),
        Entity_FI = Table.ReplaceValue(
            Entity_Default,
            "--",
            each if Text.Contains([板块], "金融") then "【】" else "--",
            Replacer.ReplaceValue,
            {"板块二级", "板块三级"}
        ),
        #"tbl PastName" =
            let
                source = CWB("mPastName"),
                cn_source = Table.ColumnNames(source),
                SCol = Table.SelectColumns(source, List.Select(cn_source, each Text.StartsWith(_, "修改"))),
                Dupe = Table.DuplicateColumn(SCol, "修改.曾用名", "信用主体")
            in
                Dupe,
        Revised = fx("DB_Revise")(Entity_FI, #"tbl PastName", "信用主体")
    in
        Revised
