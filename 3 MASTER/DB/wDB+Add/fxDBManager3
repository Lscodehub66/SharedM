(TableName as table, _primarykey as text) =>
    let
        SourceDB = TableName,
        CountRow = Table.RowCount(TableName),
        #"Removed Blank Rows" = Table.SelectRows(
            SourceDB, each not List.IsEmpty(List.RemoveMatchingItems(Record.FieldValues(_), {"", null}))
        ),
        // Definitions fx("DBManager")(Source,"mCredit","LIFO")
        //
        CN_Source = Table.ColumnNames(#"Removed Blank Rows"),
        HD = List.Intersect(
            {{"Check", "ISIN", "信用主体", "信用主体(备证行)", "Date", "Timestamp", "板块二级", "BISL_ROLE"}, CN_Source}
        ),
        HD_Count = List.Count(HD),
        MorW = if CountRow > 200 or List.Contains(CN_Source, "Timestamp") then "M" else "W",
        // Filter out blank rows & invoke dataformat
        NullOut = fx("Data")(#"Removed Blank Rows"),
        // Recalculate identifiers
        RIdentifier =
            if _primarykey = "Check" then
                Table.ReplaceValue(
                    NullOut,
                    each [Check],
                    each Load("fxCheck")([定价日], "yyMMdd", [ISIN], "Y"),
                    Replacer.ReplaceValue,
                    {"Check"}
                )
            else
                NullOut,
        // 添加Index辅助列，由新到旧index递增
        Indexed = Table.AddIndexColumn(RIdentifier, "Dummy" & "Index", 1, 1),
        SortedDupe =
            if HD_Count > 0 then
                Table.Buffer(
                    Table.Sort(Indexed, {{_primarykey, Order.Ascending}, {"Dummy" & "Index", Order.Ascending}})
                )
            else
                Indexed,
        RemovedDupe =
            if HD_Count = 0 or _primarykey = "Timestamp" or _primarykey = "" then
                Table.Distinct(SortedDupe)
            else
                Table.Distinct(SortedDupe, {_primarykey}),
        // Weekly
        // Remember to check the referenced name if you revise previous steps **Referenced Dummy Step **
        _CROSSROAD = if MorW = "W" then fx("UpdatorTS")(RemovedDupe) else RemovedDupe,

        BISL_对帐号 = Load("fxPRJSync")(    _CROSSROAD , "台账序号", "定价日", "PRJ对账号"),
        #"Invoked Entity Manager" =
            if List.Contains(HD, "板块二级") and MorW = "M" then
                fx("DB_Entity")(BISL_对帐号)
            else
                BISL_对帐号,
        #"Replaced 债券简称" =
            if Table.HasColumns(#"Invoked Entity Manager", {"债券简称", "Check", "Timestamp"}) then
                let
                    GetShortName = fx(
                        "Merge"
                    )(
                        #"Invoked Entity Manager", CWB("deal_master"), "Check", "Check", {"交易简称"}, {"Dummy.债券简称"}
                    ),
                    Replaced1 = Table.ReplaceValue(
                        GetShortName,
                        each [债券简称],
                        each if not List.Contains({null, "", "--"}, [Dummy.债券简称]) then [Dummy.债券简称] else [债券简称],
                        Replacer.ReplaceValue,
                        {"债券简称"}
                    )
                in
                    Replaced1
            else
                #"Invoked Entity Manager",
   
        PresentedNull = fx("PresentNoData")( #"Replaced 债券简称"),
        //Sort as last step
        RCol = fx("RCol")(PresentedNull),
        Sorted = fx("SortNIndex")(RCol)
    in
        Sorted
