(TableName as table) =>
    let
        Source = TableName,
        CN_Source = List.Buffer(Table.ColumnNames(Source)),
        Fn = List.Accumulate(
            {
                {"IS_CLIENT", List.Buffer(CWB("Directory")[信用主体]), "信用主体"},
                {"IS_SBLC", List.Buffer(CWB("deal_master")[备证行]), "信用主体简称"},
                {"IS_DEFAULT", List.Buffer(CWB("DMI_Default")[发行人名称]), "信用主体"},
                {"IS_ISSUER", List.Buffer(CWB("deal_master")[信用主体]), "信用主体"},
                {"IS_KYC_DONE", List.Buffer(Table.SelectRows(Load("合规_23_22"), each [KYC] = "Y")[PRJ对账号]), "PRJ对账号"}
            },
            Source,
            (x, y) =>
                if List.Contains(CN_Source, y{0}) then
                    Table.ReplaceValue(
                        x,
                        "",
                        each if List.Contains(y{1}, Record.Field(_, y{2})) then "Y" else "",
                        Replacer.ReplaceValue,
                        {y{0}}
                    )
                else
                    x
        ),
        Merged = fx("Merge")(Fn, Load("合规_23_22"), "PRJ对账号", "PRJ对账号", {"公盘地址"}, {"公盘地址"}),
        Replaced = Table.ReplaceValue(
            Merged,
            "",
            each if [公盘地址] <> "" then Text.AfterDelimiter([公盘地址], "\", {1, RelativePosition.FromEnd}) else "",
            Replacer.ReplaceValue,
            {"FolderPath"}
        ),
        Removed =
            if List.Contains(CN_Source, "FolderPath") then
                Table.RemoveColumns(Replaced, {"公盘地址"}, MissingField.Ignore)
            else
                Fn,
                Merged1 = fx("Merge")(Removed , try CWB("DMI_Default") otherwise Load("DMI_Default"), "信用主体", "发行人名称", {"首次违约日"}, {"违约日"}),
        Replaced1 = Table.ReplaceValue(
            Merged1,
            null,
            each if [违约日] <> null then [违约日] else null,
            Replacer.ReplaceValue,
            {"首次违约日"}
        ),
        Removed1 =
            if List.Contains(CN_Source, "首次违约日") then
                Table.RemoveColumns(Replaced1, {"违约日"}, MissingField.Ignore)
            else
                  Removed

    in
        Removed1
