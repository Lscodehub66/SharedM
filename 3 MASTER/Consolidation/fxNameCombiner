(TableName as table) =>
    let
        Source = TableName,
        RefCol = {"信用主体", "通过分行", "增发判定", "货币", "结构", "票息", "年期", "兑付日", "品种主题", "PROJECT_CODE", "期数", "信用主体简称"},
        NewCol = {"Short Name", "信用主体(备证行)", "PROJECT_NAME", "交易简称", "债券简称"},
        GNG = "Go",
        // Prep
        NulltoBlank = Table.ReplaceValue(
            Source,
            null,
            "",
            Replacer.ReplaceValue,
            List.Intersect(
                {
                    {
                        "期限备注",
                        "年期",
                        "增发判定",
                        "IS_LEAD",
                        "BISL_ROLE",
                        "OTHER_ROLE",
                        "结构",
                        "备证行",
                        "通过分行",
                        "品种主题",
                        "PROJECT_CODE",
                        "期数",
                        "信用主体简称"
                    },
                    Table.ColumnNames(Source)
                }
            )
        ),
        RemovedCol = Table.RemoveColumns(NulltoBlank, List.Distinct(NewCol), MissingField.Ignore),
        //PROJECT_NAME合成
        Code_project = Table.AddColumn(
            RemovedCol,
            "PrjCode",
            each if not List.Contains({"", null, " ", "--"}, [PROJECT_CODE]) then [PROJECT_CODE] else "",
            type text
        ),
        Vol_project = Table.AddColumn(
            Code_project, "PrjVol", each if not List.Contains({"", null, " ", "--"}, [期数]) then " " & [期数] else "",
            type text
        ),
        Tap_project = Table.AddColumn(
            Vol_project, "PrjTap", each if Text.Contains(([增发判定] ?? ""), "增发") then " Tap" else "", type text
        ),
        Combined_project = Table.CombineColumns(
            Tap_project,
            {"PrjCode", "PrjVol", "PrjTap"},
            Combiner.CombineTextByDelimiter("", QuoteStyle.None),
            "PROJECT_NAME"
        ),
        //信用主体(备证行) 合成
        Tap_ShortName = Table.AddColumn(
            Combined_project, "DummySNTap", each if Text.Contains(([增发判定] ?? ""), "增发") then "*" else "", type text
        ),
        RevalidateBranch = Table.ReplaceValue(
            Tap_ShortName,
            each [通过分行],
            each
                if not Text.Contains([信用主体], "银行") and [通过分行] <> "" and not Text.Contains([通过分行], "(") then
                    ""
                else
                    [通过分行],
            Replacer.ReplaceValue,
            {"通过分行"}
        ),
        #"Added ClientName" = Table.AddColumn(
            RevalidateBranch,
            "Short Name",
            each
                if Text.Contains([通过分行], "分行") and Text.Length([信用主体简称]) = 4 then
                    Text.Range([信用主体简称], 0, 1) & Text.Range([信用主体简称], 2, 1) & [通过分行]
                else
                    [信用主体简称] & [通过分行],
            type text
        ),
        TFEntity = fx("Replace")(#"Added ClientName", {"Short Name"}, "分行简称"),
        NCEntity = Table.AddColumn(
            TFEntity,
            "信用主体(备证行)",
            each
                if List.Contains({null, "", "--"}, [备证行]) then
                    [Short Name] & [DummySNTap]
                else
                    [Short Name] & "(" & Text.BeforeDelimiter([备证行], "(") & ")" & [DummySNTap],
            type text
        ),
        //交易简称命名
        SecEntity = Table.AddColumn(
            NCEntity,
            "交易简称",
            each
                if (Text.Contains([结构], "备证") or Text.Contains([结构], "SBLC")) and Text.Contains([#"信用主体(备证行)"], "*")
                then
                    [信用主体简称] & "SBLC*"
                else if Text.Contains([结构], "备证") or Text.Contains([结构], "SBLC") then
                    [信用主体简称] & "SBLC"
                else
                    [#"信用主体(备证行)"]
        ),
        CCYCPN = Table.ReplaceValue(
            SecEntity,
            each [交易简称],
            each
                if [货币细分] = "FTZ" then
                    [交易简称] & " FTZ " & [票息]
                else if [品种主题] = "AT1" or [品种主题] = "T2" then
                    [交易简称] & " " & [品种主题] & " " & [票息]
                else if Text.Contains([票息], "+") then
                    [交易简称] & " " & [货币] & " FRN"
                else
                    [交易简称] & " " & [货币] & " " & [票息],
            Replacer.ReplaceValue,
            {"交易简称"}
        ),
        Maturity = Table.ReplaceValue(
            CCYCPN,
            each [交易简称],
            each
                if [兑付日] = null then
                    [交易简称]
                else if Text.Contains([年期], "P") or Text.Contains([品种主题], "Perp", Comparer.OrdinalIgnoreCase) then
                    [交易简称] & " Perp"
                else
                    [交易简称] & " " & Date.ToText([兑付日], "yy") & "'",
            Replacer.ReplaceValue,
            {"交易简称"}
        ),
        TFSecName = Table.TransformColumns(
            Maturity, {{"交易简称", each if Text.Contains(_, "*") then Text.Replace(_, "*", "") & " Tap" else _}}
        ),
        DoubleSpace =
            if GNG = "NoGo" then
                Table.SelectColumns(Source, List.Distinct(Table.ColumnNames(Source) & NewCol), MissingField.UseNull)
            else
                Table.ReplaceValue(
                    TFSecName, "  ", " ", Replacer.ReplaceValue, {"PROJECT_NAME", "交易简称", "信用主体(备证行)"}
                )
    in
        DoubleSpace
