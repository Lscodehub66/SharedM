(TableName as table, optional inputNewCol as list, optional smallerslicer as nullable number) =>
    let
        HD_S = {"Base Currency", "USD_Mature"},
        NewCol = if inputNewCol = null or inputNewCol = {null} then {"余额亿美元", "余额美元规模分类"} else inputNewCol,
        Count = List.Count(NewCol),
        //FX
        USDFXtbl =
            let
                FXSource = Table.Buffer(CWB("FXtbl")),
                Renamed = Table.RenameColumns(FXSource, {{"USD", "USD_Mature"}}, MissingField.Ignore),
                #"Filtered Rows" = Table.SelectRows(Renamed, each ([YYQQ] = "24Q1")),
                #"Removed Other Columns" = Table.SelectColumns(#"Filtered Rows", HD_S)
            in
                Table.Buffer(#"Removed Other Columns"),
        //FX
        Source = TableName,
        Prep = Table.RemoveColumns(Source, NewCol, MissingField.Ignore),
        #"Joined FX" =
            if Table.HasColumns(Source, "USD_Mature") then
                Prep
            else
                Table.Join(Prep, {"货币"}, USDFXtbl, {"Base Currency"}, JoinKind.LeftOuter),
        Replaced = fx("ReplaceNullOutstanding")(#"Joined FX"),
        #"Inserted Multiplication" = Table.AddColumn(Replaced, NewCol{0}, each [USD_Mature] * [债券余额], type number),
        #"Rounded Off" = Table.TransformColumns(
            #"Inserted Multiplication", {{NewCol{0}, each Number.Round(_, 3), type number}}
        ),
        Slicer_Size0 = fx("GroupRange")(#"Rounded Off", NewCol, {"--", 2, 3, 5}, "亿"),
        Slicer_Size = Table.ReplaceValue(
            Slicer_Size0,
            "≤2亿",
            each if Record.Field(_, NewCol{0}) <= 1 then "≤1亿" else "1-2亿",
            Replacer.ReplaceValue,
            {NewCol{1}}
        ),
        Crossroad = if Count = 1 then #"Rounded Off" else if smallerslicer <> null then Slicer_Size else Slicer_Size0,
        #"Removed Columns" = fx("RCol")(Crossroad, null, HD_S & {"Dummy" & NewCol{0}}),
        Output = if Table.HasColumns(Source, "债券余额") then #"Removed Columns" else Source
    in
        Output
