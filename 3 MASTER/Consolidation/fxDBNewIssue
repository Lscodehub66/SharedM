//let MonitorName = "ESG", LoadCode = Load("fxDBNewIssue","Code"), EVA = Expression.Evaluate(LoadCode ,#shared)(deal_master,MonitorName) in EVA
(SourceTable as table, TableNameText as text) =>
    let
        Key = Text.Upper(Text.Trim(TableNameText)),
        //
        Source = Table.Buffer(SourceTable),
        NoHide_Source = Table.SelectRows(Source, each [删除及隐藏] <> "删除" and [删除及隐藏] <> "隐藏"),
        SourceFrom22 = Table.SelectRows(NoHide_Source, each [定价日] > Date.From("2021/12/31")),
        SourceUSDCNH = Table.SelectRows(SourceFrom22, each ([货币] = "USD" or [货币] = "CNH")),
        // ORecent
        Header0 = Get("Header_Graph_Deal_Weekly"),
        // {"Index","ISIN","定价日","信用主体(备证行)","发行评级","货币","发行规模(亿)","年期","票息","IPG","收窄基点","结构","主题","板块","地区","BOCOM_DEAL?","亿美元"  },
        SCol0 = Table.SelectColumns(SourceFrom22, Header0, MissingField.UseNull),
        // 1ESG
        Header1 = Get("HeaderESG"),
        Filtered1 = Table.SelectRows(SourceFrom22, each not List.Contains({null, "", "--"}, [ESG])),
        SCol1 = Table.SelectColumns(Filtered1, Header1, MissingField.UseNull),
        // 2DimSum
        Header2 = Get("HeaderDimSum"),
        Filtered2 = Table.SelectRows(SourceFrom22, each [货币细分] = "点心债"),
        SCol2 = Table.SelectColumns(Filtered2, Header2, MissingField.UseNull),
        // 2a DimSum & FTZ
        Filtered2a = Table.SelectRows(SourceFrom22, each [货币] = "CNH"),
        SCol2a = Table.SelectColumns(Filtered2a, Header2 & {"货币细分"}, MissingField.UseNull),
        // 3IGLGFV
        Header3 = Get("HeaderIGLGFV"),
        Filtered3 = Table.SelectRows(SourceFrom22, each [是否IG] <> "--" and [投标资历分类] <> "--" and [货币] <> "CNH"),
        SCol3 = Table.SelectColumns(Filtered3, Header3, MissingField.UseNull),
        //4SBLC
        Header4 = Get("HeaderSBLC"),
        Filtered4 = Table.SelectRows(SourceFrom22, each Text.Contains([结构], "备证")),
        SCol4 = Table.SelectColumns(Filtered4, Header4, MissingField.UseNull),
        // 5FI
        Header5 = Get("HeaderFI"),
        Filtered5 = Table.SelectRows(SourceFrom22, each Text.Contains([板块], "金")),
        SCol5 = Table.SelectColumns(Filtered5, Header5, MissingField.UseNull),
        // 6FTZ
        Header6 = Get("HeaderFTZ"),
        Filtered6 = Table.SelectRows(Source, each [货币细分] = "自贸债"),
        SCol6 = Table.SelectColumns(Filtered6, Header6, MissingField.UseNull),
        //8USDCNH_TIGHTEN
        Header8 = {
            "Index",
            "ISIN",
            "定价日",
            "信用主体简称",
            "信用主体",
            "货币",
            "亿美元",
            "评级分类",
            "币种规模",
            "发行品种",
            "板块",
            "收窄基点",
            "YYQQ",
            "Check"
        },
        Filtered8 = Table.SelectRows(SourceUSDCNH, each not List.Contains({null, "", "--"}, [收窄基点])),
        SCol8 = Table.SelectColumns(Filtered8, Header8, MissingField.UseNull),
        //9Mature2024
        Header9 = Get("Header_Maturity2024"),
        //  {"Index", "ISIN", "信用主体(备证行)","发行评级","货币", "发行规模(亿)","债券余额","兑付日","期限","票息", "发行品种", "中资属性","板块", "地区","城市","IS_CLIENT", "IS_BISLDeal", "信用主体","亿美元","定价日" ,"Check"},
        Filtered9 = Table.SelectRows(NoHide_Source, each Date.Year([兑付日]) = 2024  
        and List.Contains({null,""},[IS_DEFAULT]) and not List.Contains({"中华人民共和国财政部"},[信用主体])
        and [债券余额]>0),
        BISLCheck = List.Buffer((try CWB("Directory") otherwise fx("Excel")("DEALSMASTER_=DIRECTORY"))[Check]),
        BISL_DEAL = Table.AddColumn(
            Filtered9, "IS_BISLDeal", each if List.Contains(BISLCheck, [Check]) then "Y" else "", type text
        ),
        Replaced_ISIN = Table.ReplaceValue(
            BISL_DEAL,
            each [ISIN],
            each if Text.Contains([发行方式], "临时") then [OTHER_ISIN] else [ISIN],
            Replacer.ReplaceValue,
            {"ISIN"}
        ),
        SCol9 = Table.SelectColumns(Replaced_ISIN, Header9, MissingField.UseNull),
        //******
        Output =
            if Text.Contains(Key, "ESG") then
                Table.TransformColumns(SCol1, {{"ESG主题", each if not Text.Contains(_, "债") then _ & "债券" else _}})
            else if Text.Contains(Key, "DIMSUM") and Text.Contains(Key, "FTZ") then
                SCol2a
            else if Text.Contains(Key, "DIMSUM") then
                Table.ReplaceValue(
                    SCol2,
                    each [主题],
                    each if [主题] = "点心债" then "--" else Text.Replace([主题], "点心债", "债券"),
                    Replacer.ReplaceValue,
                    {"主题"}
                )
            else if Text.Contains(Key, "IGLGFV") then
                SCol3
            else if Text.Contains(Key, "SBLC") then
                SCol4
            else if Text.Contains(Key, "FI") then
                SCol5
            else if Text.Contains(Key, "FTZ") then
                Table.ReplaceValue(
                    SCol6,
                    each [主题],
                    each if [主题] = "明珠债" then "--" else Text.Replace([主题], "明珠债", "债券"),
                    Replacer.ReplaceValue,
                    {"主题"}
                )
            else if Text.Contains(Key, "USDCNH_DATA") then
                SourceUSDCNH
            else if Text.Contains(Key, "USDCNH_TIGHTEN") then
                Table.TransformColumnTypes(SCol8, {{"收窄基点", type number}})
            else if Text.Contains(Key, "MATURITY2024") then
                SCol9
            else
                SCol0,
        //******FinalSort*****
        FnSort = fx("SortNIndex")(Output)
    in
        FnSort
