(TableName as table, optional _inputkey as nullable text) =>
    let
        Key = Text.Upper(Text.Trim(_inputkey ?? "")),
        Source = TableName,
        ReplacedCCY = Table.ReplaceValue(Source, "美元债", "", Replacer.ReplaceValue, {"货币细分"}),
        Added = fx("AddCOMPARER")(ReplacedCCY),
        NthMandate = fx("NthMandated")(Added, {"YYMMDD", "信用主体简称"}),
        #"Count Multi-Tranche" = fx("AddMultiCount")(NthMandate),
        #"Merged Queries1" = fx(
            "Merge"
        )(
            #"Count Multi-Tranche", Load("DimTime"), "定价会时间", "Time", {"DayPeriod"}, {"定价会时段"}
        ),
        ExeTime =
            if Table.HasColumns(#"Merged Queries1", "Date_Onboarded") then
                Table.AddColumn(
                    #"Merged Queries1",
                    "执行周期",
                    each
                        if not List.Contains({[定价日], [Date_Onboarded]}, null) and [定价日] >= [Date_Onboarded] then
                            Duration.Days([定价日] - [Date_Onboarded])
                        else
                            null,
                    type number
                )
            else
                #"Merged Queries1",
        //
        #">>>Fee19" = Table.SelectColumns(Load("Fee19", "CSVDATE"), {"COMPARER", "补齐.承销费.原币"}),
        #"QRCCPL Records" =
            let
                Source = CWB("QRC_Compliance"),
                Output = Table.RenameColumns(
                    Table.SelectColumns(
                        Source,
                        {"PRJ对账号", "发行人", "发行人名称中文", "上会审批日", "Mandated Date", "是否书面委任", "KYC Deadline", "团队成员"},
                        MissingField.UseNull
                    ),
                    {{"发行人", "修改.发行人"}}
                ),
                Duped = Table.DuplicateColumn(Output, "修改.发行人", "KYC Entity Name")
            in
                Duped,
        //
        #"Merged 21Fee" = fx(
            "Merge"
        )(
            ExeTime, CWB("QRC_Income"), "PRJ对账号", "PRJ对账号", {"承销费.原币", "承销费.美元", "QRC汇率因子"}, {""}
        ),
        #"Revised 19Fee" = fx("DB_Revise")(#"Merged 21Fee", #">>>Fee19", "COMPARER"),
        #"Merged QRCCPL" = fx("DB_Revise")(#"Revised 19Fee", #"QRCCPL Records", "PRJ对账号", "RCol"),
        CN_Base = List.Buffer(CWB("BaseDirectory_CN")),
        CN_CWB = List.Buffer(CWB("Directory", "CN")),
        Reordered = Table.ReorderColumns(
            #"Merged QRCCPL", CN_Base & List.RemoveItems(CN_CWB, CN_Base), MissingField.Ignore
        ),
        SCol = Table.SelectColumns(Reordered, CN_Base, MissingField.Ignore),
        Output = if Text.Contains(Key, "BASE") then SCol else Reordered
    in
        Output
