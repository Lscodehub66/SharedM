(TableName as table) =>
    let
        Source = TableName,
        RoleCheck =
            if Table.HasColumns(Source, {"DataSource", "BISL_ROLE"}) then
                Table.ReplaceValue(
                    Source, "--", each if [DataSource] = "BBG" then "【】" else "--", Replacer.ReplaceValue, {
                        "BISL_ROLE"
                    }
                )
            else
                Source,
        Replaced =
            if Table.HasColumns(RoleCheck, {"删除及隐藏"}) then
                List.Accumulate(
                    {"mIssue", "mBond", "mStats", "mBISL", "mEXE"},
                    RoleCheck,
                    (x, y) =>
                        if Table.HasColumns(x, y) then
                            Table.ReplaceValue(
                                x,
                                each Record.Field(_, y),
                                each if [删除及隐藏] = "FIMTN" then "n/a" else Record.Field(_, y),
                                Replacer.ReplaceValue,
                                {y}
                            )
                        else
                            x
                )
            else
                RoleCheck,
        Fn = List.Accumulate(
            {{"mIssue", "Check"}, {"mBond", "ISIN"}, {"mBISL", "BISL_ROLE"}, {"mEXE", "BISL_ROLE"}},
            Replaced,
            (x, y) =>
                if not Table.HasColumns(x, y{0}) then
                    Table.AddColumn(
                        x,
                        y{0},
                        each if not List.Contains({null, "", "--"}, Record.Field(_, y{1})) then "新增" else "",
                        type text
                    )
                else
                    Table.ReplaceValue(
                        x,
                        "",
                        each if not List.Contains({null, "", "--"}, Record.Field(_, y{1})) then "新增" else "",
                        Replacer.ReplaceValue,
                        {y{0}}
                    )
        ),
        Replaced1 =
            if Table.HasColumns(Fn, {"mStats", "最终账簿(亿)", "地区分布"}) then
                Table.ReplaceValue(
                    Fn,
                    "",
                    each
                        if
                            not List.Contains({null, "【】", ""}, [#"最终账簿(亿)"])
                            and not List.Contains({null, "【】", ""}, [地区分布])
                        then
                            "新增"
                        else
                            "",
                    Replacer.ReplaceValue,
                    {"mStats"}
                )
            else
                Fn,
        Replaced2 =
            if Table.HasColumns(Fn, {"mChart", "IS_FIMTN", "货币"}) then
                Table.ReplaceValue(
                    Replaced1,
                    "",
                    each
                        if List.Contains({"【Y】", "Y"}, [IS_FIMTN]) and List.Contains({"USD", "CNH"}, [货币]) then
                            "新增"
                        else
                            "n/a",
                    Replacer.ReplaceValue,
                    {"mChart"}
                )
            else
                Replaced1,
        DM = {"mCredit", "债券备注"},
        #"Operational Columns" = List.Accumulate(
            DM, Replaced2, (x, y) => if not Table.HasColumns(x, y) then Table.AddColumn(x, y, each "", type text) else
                x
        ),
        Replaced3 =
            if Table.HasColumns(Fn, {"mCredit"}) then
                Table.ReplaceValue(#"Operational Columns", "", "新增", Replacer.ReplaceValue, {"mCredit"})
            else
                #"Operational Columns"
    in
        Replaced3
