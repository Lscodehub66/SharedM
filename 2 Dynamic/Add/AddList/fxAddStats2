(TableName as table) =>
    let
        Source = TableName,
        NewCol = {"最终账簿(亿)", "账户数", "地区分布", "类型分布"},
        RefCol = {"认购额(亿)", "投资者账户数", "投资者分布", "投资者类型"},
        Zipped = List.Zip({RefCol, NewCol}),
        PrepNumCol = List.Accumulate(
            {"认购额(亿)", "发行规模(亿)", "投资者账户数"},
            Source,
            (x, y) => if not Table.HasColumns(x, y) then Table.AddColumn(x, y, each null, type number) else x
        ),
        Replaced = Table.ReplaceValue(PrepNumCol, null, "", Replacer.ReplaceValue, {"投资者类型", "投资者分布"}),
        DupeCol = List.Accumulate(Zipped, Replaced, (x, y) => Table.DuplicateColumn(x, y{0}, y{1})),
        CrossRoad = if Table.HasColumns(Source, NewCol) then Source else DupeCol,
        TF = Table.TransformColumns(
            CrossRoad,
            {
                {"地区分布", each Text.Upper(Text.Trim(_)), type text},
                {"类型分布", each Text.Upper(Text.Trim(_)), type text},
                {"最终账簿(亿)", each Number.Round(_, 1), type number},
                {"账户数", each Number.Round(_, 1), type number}
            }
        ),
        Cleaned = fx("Replace")(TF, {"地区分布", "类型分布"}, "投资者"),
        Rp = Table.ReplaceValue(Cleaned, "，，", "，", Replacer.ReplaceText, {"地区分布", "类型分布"}),
        Rp1 = Table.ReplaceValue(
             Rp , "", each if [#"最终账簿(亿)"] <> null then "【】" else "", Replacer.ReplaceValue, {"地区分布", "类型分布"}
        )
    in
        Rp1
