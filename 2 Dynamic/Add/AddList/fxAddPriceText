(TableName as table) =>
    let
        Source = TableName,
        RefCol = {"定价息差"},
        Prep2 = List.Accumulate(
            RefCol,
            Source,
            (x, y) => if not Table.HasColumns(x, y) then Table.AddColumn(x, y, each "【Missing】" & y, type text) else x
        ),
        AIPT_Bmk = Table.AddColumn(
            Prep2,
            "IPT_BMK",
            each
                if Text.Contains([IPT], "+") then
                    Text.BeforeDelimiter([IPT], "+")
                else if Text.Contains([IPT], "%") then
                    "%"
                else if Text.Contains([IPT], "【") then
                    [IPT]
                else
                    "",
            type text
        ),
        AFPT_Bmk = Table.AddColumn(
            AIPT_Bmk,
            "FPT_BMK",
            each
                if Text.Contains([FPT2], "%") then
                    "%"
                else if Text.Contains([FPT2], "+") and [IPT_BMK] <> "" then
                    [IPT_BMK]
                else if Text.Contains([FPT2], "+") then
                    Text.BeforeDelimiter([FPT2], "+")
                else if Text.Contains([FPT2], "【") then
                    [FPT2]
                else
                    ""
        ),
        APrice_Bmk = Table.AddColumn(
            AFPT_Bmk,
            "定价_BMK",
            each
                if [浮息_BMK] <> "" then
                    [浮息_BMK]
                else if [FPT_BMK] = "%" and [IPT_BMK] <> "%" and [IPT_BMK] <> "" then
                    "%" & "(IPG:" & [IPT_BMK] & ")"
                else if [FPT_BMK] <> "" then
                    [FPT_BMK]
                else if [IPT] <> "" then
                    [IPT_BMK]
                else
                    "%"
        ),
        RP_SpdFRN = Table.ReplaceValue(
            APrice_Bmk, each [定价息差], each if [浮息_SPD] <> "" then [浮息_SPD] else [定价息差], Replacer.ReplaceValue, {
                "定价息差"
            }
        ),
        RP_SpdFPT = Table.ReplaceValue(
            Table.ReplaceValue(
                RP_SpdFRN,
                each [定价息差],
                each
                    if [定价_BMK] = "MS" or [IPT_BMK] = "MS" and not List.Contains({null, "", "【Missing】定价息差"}, [定价息差])
                    then
                        ""
                    else
                        [定价息差],
                Replacer.ReplaceValue,
                {"定价息差"}
            ),
            "",
            each [FPT_SPD假设窄端],
            Replacer.ReplaceValue,
            {"定价息差"}
        ),
        ToPercent = Table.TransformColumns(
            fx("Data")(RP_SpdFPT),
            {{"IPT_YIELD", each _ / 100, type number}, {"FPT_YIELD", each _ / 100, type number}}
        ),
        ToPercent_DMI100 = Table.AddColumn(ToPercent, "票息100DMI", each [票息_DMI] / 100, type number),
        PYieldNum = Table.AddColumn(
            ToPercent_DMI100,
            "定价YIELD_Num",
            each
                if [IPT_BMK] = "%" and [WPIR] <> null then
                    [FPT_YIELD] - ([WPIR] / 10000)
                else if [IPT_BMK] = "%" and [WPIR] = null and [FPT_BMK] = "%" then
                    [FPT_YIELD]
                else
                    [票息100DMI]
        ),
        TigtenNum = Table.AddColumn(
            PYieldNum,
            "收窄基点",
            each
                if [IPT] <> "" and [定价息差] <> null then
                    [IPT_SPD] - [定价息差]
                else if [定价_BMK] = "%" and [IPT] <> "" then
                    ([IPT_YIELD] - [定价YIELD_Num]) * 10000
                else
                    null,
            type number
        ),
        RoundedOff = Table.TransformColumns(
            TigtenNum,
            {
                {"定价YIELD_Num", each Number.Round(_, 4), type number},
                {"IPT_YIELD", each Number.Round(_, 4), type number},
                {"FPT_YIELD", each Number.Round(_, 4), type number},
                {"收窄基点", each Number.Round(_, 1), type number}
            }
        ),
        IPT区间描述 = Table.AddColumn(
            RoundedOff,
            "IPG",
            each
                if [IPT_BMK] = "%" then
                    Number.ToText([IPT_YIELD], "0.000%") & "a"
                else if [IPT_BMK] <> "" and [IPT_SPD] <> null then
                    [IPT_BMK] & "+" & Number.ToText([IPT_SPD], "0.0") & "a"
                else
                    [IPT_BMK]
        ),
        NoWIRP = Table.AddColumn(
            IPT区间描述,
            "FPG1",
            each
                if Text.Contains([FPT_BMK], "【") then
                    [FPT_BMK]
                else if [FPT_YIELD上限] <> null then
                    Number.ToText([FPT_YIELD], "0.000%") & "-" & Number.ToText([FPT_YIELD上限], "0.000%")
                else if [FPT_SPD上限] <> null then
                    [FPT_BMK] & "+" & Number.ToText([FPT_SPD], "0.0") & "-" & Number.ToText([FPT_SPD上限], "0.0")
                else if [FPT_BMK] <> "" and [FPT_YIELD] <> null then
                    Number.ToText([FPT_YIELD], "0.000%")
                else if [FPT_BMK] <> "" and [FPT_SPD] <> null then
                    [FPT_BMK] & "+" & Number.ToText([FPT_SPD], "0.0")
                else
                    ""
        ),
        FPGwithWIRP = Table.AddColumn(
            NoWIRP,
            "FPG",
            each
                if [WPIR] <> null and [FPT_BMK] = "%" then
                    [FPG1] & "(+/-" & Number.ToText([WPIR], "0.0") & ")"
                else if [WPIR] <> null then
                    [FPG1] & "(+/-" & Number.ToText([WPIR], "0.0") & ")"
                else
                    [FPG1]
        ),
        PricedwSPD = Table.AddColumn(
            FPGwithWIRP,
            "定价",
            each
                if [定价息差] <> null and not Text.Contains([定价_BMK], "%") then
                    [定价_BMK] & "+" & Number.ToText([定价息差], "0.0")
                else if [FPG] = "【】" then
                    "【】"
                else
                    ""
        ),
        票息Percent = Table.AddColumn(
            PricedwSPD,
            "票息",
            each
                if
                    [计息基准] <> ""
                    or Text.Contains([IPT_BMK], "SOFR")
                    or Text.Contains([FPT_BMK], "SOFR")
                    or Text.Contains([定价_BMK], "SOFR")
                then
                    [定价]
                else if [票息100DMI] = null then
                    ""
                else
                    Number.ToText([票息100DMI], "0.000%"),
            type text
        ),
        fxNumText = fx("NumText"),
        Custom1 = Table.TransformColumns(
            票息Percent,
            {
                {"IPG", each fxNumText(_)},
                {"票息", each fxNumText(_)},
                {"FPG", each fxNumText(_)},
                {"定价", each fxNumText(_)}
            }
        ),
        Renamed = Table.RenameColumns(Custom1, {{"定价_BMK", "定价基准"}}, MissingField.Ignore),
        Replaced11 = List.Accumulate(
            {"IPG", "FPG", "票息", "定价","定价基准"},
            Renamed,
            (x, y) =>
                Table.ReplaceValue(
                    x,
                    "BKBM",
                    each if Text.StartsWith(Record.Field(_, y), "BKBM") then "3mBKBM" else "BKBM",
                    Replacer.ReplaceText,
                    {y}
                )
        )
    in
        Replaced11
