(TableName as table) =>
    let
        Source = TableName,
        PrepNumCol = List.Accumulate(
            {"认购额(亿)", "发行规模(亿)", "投资者账户数"},
            Source,
            (x, y) => if not Table.HasColumns(x, y) then Table.AddColumn(x, y, each null, type number) else x
        ),
        Replaced = Table.ReplaceValue(PrepNumCol, null, "", Replacer.ReplaceValue, {"投资者类型", "投资者分布"}),
        Division = Table.AddColumn(
            Replaced, "超额认购倍数", each if [#"认购额(亿)"] <> null then ([#"认购额(亿)"] / [#"发行规模(亿)"]) else null, type number
        ),
        RoundedOff = Table.TransformColumns(
            Division,
            {
                {"认购额(亿)", each Number.Round(_, 1), type number},
                {"超额认购倍数", each Number.Round(_, 1), type number},
                {"投资者账户数", each Number.Round(_, 0), type number}
            }
        ),
        AddCol0 = Table.AddColumn(
            RoundedOff, "账户描述", each if [投资者账户数] <> null then "超" & Number.ToText([投资者账户数]) & "个账户参与，" else ""
        ),
        AddCol = Table.AddColumn(
            AddCol0,
            "认购额描述",
            each
                if [#"认购额(亿)"] <> null then
                    "认购额超" & Number.ToText([#"认购额(亿)"]) & "亿" & [货币] & "，超额认购约" & Number.ToText([超额认购倍数]) & "倍；"
                else
                    ""
        ),
        ReplacedCrcy = fx("Replace")(AddCol, {"认购额描述"}, "货币"),
        Merged1 = Table.TransformColumns(
            Table.CombineColumns(
                ReplacedCrcy, {"账户描述", "认购额描述"}, Combiner.CombineTextByDelimiter("", QuoteStyle.None), "认购描述"
            ),
            {{"认购描述", each fx("Clean")(_, {".0倍", "0倍"}, {"倍", "倍"})}}
        ),
        DupeCol = Table.DuplicateColumn(Table.DuplicateColumn(Merged1, "投资者分布", "地区分布描述"), "投资者类型", "类型分布描述"),
        Cleaned = fx("Replace")(DupeCol, {"地区分布描述", "类型分布描述"}, "投资者"),
        TF = Table.TransformColumns(
            Cleaned,
            {
                {"地区分布描述", each if (_ ?? "") <> "" then "地区分布：" & _ & "；" else "", type text},
                {"类型分布描述", each if (_ ?? "") <> "" then "类型分布：" & _ & "；" else "", type text}
            }
        ),
        Merged = Table.AddColumn(
            TF, "Stats", each fx("PowerTrim")(Text.Combine({[认购描述], [地区分布描述], [类型分布描述]}, "#(lf)"), "#(lf)"), type text
        ),
        RemovedEnd = Table.TransformColumns(
            Merged,
            {
                {
                    "Stats",
                    each if Text.EndsWith(_, "；") then Text.BeforeDelimiter(_, "；", {0, RelativePosition.FromEnd}) else
                        _,
                    type text
                }
            }
        ),
        SelectedCol = Table.SelectColumns(RemovedEnd, List.Distinct(Table.ColumnNames(Source) & {"Stats"}))
    in
        SelectedCol
