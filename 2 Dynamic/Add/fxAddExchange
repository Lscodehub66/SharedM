//
(TableName as table) =>
    let
        Source = TableName,
        #"Duplicated Column" = Table.DuplicateColumn(Source, "发行场所", "上市"),
        Replaced = fx("Replace")(#"Duplicated Column", {"上市"}, "上市地"),
        #"Extracted After StartsWith" = Table.TransformColumns(
            Replaced, {{"上市", each if Text.StartsWith(_, ";") then Text.AfterDelimiter(_, ";") else _, type text}}
        ),
        #"Extracted Before EndsWith" = Table.TransformColumns(
            #"Extracted After StartsWith",
            {
                {
                    "上市",
                    each if Text.EndsWith(_, ";") then Text.BeforeDelimiter(_, ";", {0, RelativePosition.FromEnd}) else
                        _,
                    type text
                }
            }
        ),
        Splitted = Table.TransformColumns(
            #"Extracted Before EndsWith",
            {{"上市", each List.Distinct(List.RemoveItems(Text.Split(_, ";"), {""})), type text}}
        ),
        SortList = List.Buffer(
            {
                "港交所",
                "新交所",
                "澳交所",
                "伦交所",
                "伦交所(SBM)",
                "卢交所",
                "迪拜",
                "法兰克福",
                "中欧",
                "泛欧",
                "纽约泛欧",
                "纽约",
                "澳洲",
                "台北",
                "台湾",
                "欧盟MTF",
                "欧盟TLX",
                "爱尔兰",
                "汉堡",
                "瑞士",
                "布达佩斯",
                "汉诺威",
                "沙特",
                "开曼",
                "马来西亚"
            }
        ),
        Sorted = Table.TransformColumns(
            Splitted,
            {
                {
                    "上市",
                    each
                        List.Sort(
                            _, (a, b) => Value.Compare(List.PositionOf(SortList, a), List.PositionOf(SortList, b))
                        )
                }
            }
        ),
        Recombined = Table.TransformColumns(
            Sorted, {{"上市", each Text.Combine(List.Transform(_, Text.From), ";"), type text}}
        ),
        #"Removed Strange Exchange" = fx("ClearFrankfurt")(Recombined)
    in
        #"Removed Strange Exchange"
