// let LoadCode = Load("Filtered_Add","Code"), EVA = Expression.Evaluate(LoadCode ,#shared) in EVA
let
    Source1 = CWB("DLD_Conso", "1"),
    Filtered = Table.SelectRows(
        Source1,
        each List.Contains({null, "", "--"}, [Filter_Bond]) and List.Contains({null, "", "--"}, [Filter_Exch])
    ),
    Custom1 = Table.AddColumn(Filtered, "删除及隐藏", each if [IS_FIMTN] = "Y" or [IS_FIMTN] = "【Y】" then "FIMTN" else ""),
    Replaced = Table.ReplaceValue(
        Custom1, "新增", each if [删除及隐藏] = "FIMTN" then "n/a" else "新增", Replacer.ReplaceValue, {"mBond", "mIssue"}
    ),
    #"Added BISL_ROLE" =
        if not Table.HasColumns(Replaced, "BISL_ROLE") then
            Table.AddColumn(
                Replaced,
                "BISL_ROLE",
                each
                    if Text.Contains([联席全球协调人], "BOCOM") then
                        "JGC"
                    else if Text.Contains([牵头经办人], "BOCOM") then
                        "JBR"
                    else if Text.Contains([联席全球协调人], "【") then
                        "【】"
                    else
                        "--"
            )
        else
            Replaced,
    BBG_HD = Get("Header_Exp_Dynamic_BBG"),
    BBG_HDExp = List.Transform(BBG_HD, each if List.Contains({"计息基准", "SBLC提供者"}, _) then "Dummy" & _ else _),
    BBG_Merged = fx("Merge")(#"Added BISL_ROLE", CWB("DLD_BBG_Corp"), "Check", "Check", BBG_HD, BBG_HDExp),
    #"CrossChecked BBG" = fx("CrossCheck")(BBG_Merged),
    // End of MultiSource
    #"//GNG_Multi" = if fx("Parameter")("MultiSource") = "ON" then "GO" else "NO-GO",
    Crossroad =
        if #"//GNG_Multi" <> "GO" then
            Table.RenameColumns(#"Added BISL_ROLE", {{"债项评级", "发行评级"}, {"穆迪/标普", "主体评级"}}, MissingField.UseNull)
        else
            #"CrossChecked BBG",
    Custom2 = fx("RCol")(Crossroad),
    Clean_Structure = fx("Replace")(Table.DuplicateColumn(Custom2, "发行架构", "结构"), {"结构"}, "结构"),
    Added = List.Accumulate(
        {
            "Reopen",
            "SBLC",
            "Exchange",
            "BankBranch",
            "BBGFX",
            "Stats2",
            "BCM",
            "FRNString",
            "CpnString",
            "IPGString",
            "FPGString",
            "Price_FPGRange",
            "SpdTighten",
            "PtTighten",
            "Tenor",
            "ESGMore"
        },
        Clean_Structure,
        (x, y) => fx("Add" & y)(x)
    ),
    Replaced2 = Table.ReplaceValue(
        Added,
        each [债券简称],
        each
            if List.Contains({null, "", "--"}, [债券简称]) and not List.Contains({null, "", "--"}, [信用主体]) then
                [信用主体]
            else
                [债券简称],
        Replacer.ReplaceValue,
        {"债券简称"}
    ),
    DM = {"mIssue", "mStats", "mBISL", "mEXE", "mCredit"},
    #"Operational Columns" = List.Accumulate(
        DM, Replaced2, (x, y) => if not Table.HasColumns(x, y) then Table.AddColumn(x, y, each "", type text) else x
    ),
    WeeklyAddTapSwith = CWB("WeeklyAddTap"),
    Appended =
        if WeeklyAddTapSwith = "OFF" then
            #"Operational Columns"
        else
            fx("Combine")({#"Operational Columns", Load("wAddTapPQ")}, {null}, {0}),
    //
    HD2 = {"Index", "债券简称", "删除及隐藏", "mIssue", "mBond", "mStats", "mBISL", "mEXE", "mCredit"},
    //  & {"Filter_Credit", "Filter_Bond", "Filter_Exch", "IS_FIMTN"}
    Reordered = fx(
        "1st"
    )(
        try Table.ReorderColumns(Appended, CWB("Filtered_Add", "CN"), MissingField.Ignore) otherwise Appended, HD2
    ),
    Checked = fx("CheckNDupe")(Reordered),
    Sorted = fx("SortNIndex")(Checked)
in
    Sorted
