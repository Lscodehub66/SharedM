// let LoadCode = Load("DLD_Conso","Code"), EVA = Expression.Evaluate(LoadCode ,#shared) in EVA
let
    Source = Load("DLD_DMI"),
    //
    Add_BISL = Table.AddColumn(
        Source,
        "BISL_ROLE",
        each
            if Text.Contains([联席全球协调人], "BOCOM") then
                "JGC"
            else if Text.Contains([牵头经办人], "BOCOM") then
                "JBR"
            else if Text.Contains([联席全球协调人], "【") then
                "【】"
            else
                "--"
    ),
    // Multisource steps
    BBG_HD = Get("Header_Exp_Dynamic_BBG"),
    BBG_HDExp = List.Transform(BBG_HD, each if _ = "计息基准" then "Dummy" & _ else _),
    BBG_Merged = fx("Merge")(Add_BISL, Load("DLD_BBG_Corp"), "Check", "Check", BBG_HD, BBG_HDExp),
    //{"发行人成立地", "发行人主上市地", "补充OMAS updates", "定价息差", "发行评级", "主体评级","ESG_Indicator","计息基准"}
    TBC_1 = List.Accumulate(
        {{"发行评级", "债项评级"}, {"主体评级", "穆迪/标普"}},
        BBG_Merged,
        (x, y) =>
            Table.ReplaceValue(
                x,
                each Record.Field(_, y{0}),
                each
                    if Record.Field(_, y{0}) = "" and Record.Field(_, y{1}) = "" then
                        "【】/【】/【】"
                    else if Record.Field(_, y{1}) = "" then
                        "【" & Record.Field(_, y{0}) & "】"
                    else if Record.Field(_, y{0}) = "" or (Record.Field(_, y{0}) = "--/--/--" and Record.Field(_, y{1}) <> "--/--") then
                        Record.Field(_, y{1}) & "/【】"
                    else
                        Record.Field(_, y{0}),
                Replacer.ReplaceValue,
                {y{0}}
            )
    ),
    TBC_2 = Table.ReplaceValue(
        TBC_1,
        "",
        each if Record.Field(_, "Dummy" & "计息基准") <> "" then Record.Field(_, "Dummy" & "计息基准") else "",
        Replacer.ReplaceValue,
        {"计息基准"}
    ),
    // End of MultiSource
    #"//GNG_Multi" =
        if Text.Contains(Load("Controller")[Multi]{0}, "BBG") or Load("Controller")[Multi]{0} = "ON" then
            "GO"
        else
            "NO-GO",
    Crossroad =
        if #"//GNG_Multi" <> "GO" then
            Table.RenameColumns(Add_BISL, {{"债项评级", "发行评级"}, {"穆迪/标普", "主体评级"}}, MissingField.UseNull)
        else
            TBC_2,
    // facilitate FIMTN Review
    Add_FIMTN = Table.TransformColumns(Load("fxIsFIMTN")(Crossroad), {{"IS_FIMTN", each if _ = "Y" then "【Y】" else _}}),
    //
    CWBConso = Table.PrefixColumns(
        Table.SelectColumns(
            CWB("DLD_Conso"),
            {"IS_FIMTN", "IPT_DMI", "FPG_DMI", "认购额(亿)", "投资者账户数", "投资者类型", "投资者分布", "Check"},
            MissingField.UseNull
        ),
        "Dummy"
    ),
    Joined = Table.Join(Add_FIMTN, "Check", CWBConso, "Dummy.Check", JoinKind.LeftOuter),
    #"Replaced null" =  List.Accumulate({"认购额(亿)", "投资者账户数"},  Joined, (x,y) =>  Table.ReplaceValue(
       x, null, each Record.Field(_, "Dummy." & y), Replacer.ReplaceValue, {y}
    )),
     #"Replaced Blank" =  List.Accumulate({"IPT_DMI", "FPG_DMI", "投资者类型", "投资者分布"},   #"Replaced null", (x,y) =>  Table.ReplaceValue(
       x,"", each Record.Field(_, "Dummy." & y), Replacer.ReplaceValue, {y}
    )), 
    #"Replaced both Non-blank" = List.Accumulate(
        {"IS_FIMTN", "IPT_DMI", "FPG_DMI", "认购额(亿)", "投资者账户数", "投资者类型", "投资者分布"},
       #"Replaced Blank",
        (x, y) =>
            Table.ReplaceValue(
                x,
                each if not List.Contains({null,""}, Record.Field(_, y)) then Record.Field(_, y) else Record.Field(_, "Dummy." & y),
                each
                    if not List.Contains({null, ""}, y) and not List.Contains({null, ""}, "Dummy." & y) then
                        Record.Field(_, "Dummy." & y)
                    else
                        Record.Field(_, y),
                Replacer.ReplaceValue,
                {y}
            )
    ),
    Checked = fx(
        "CheckNDupe"
    )(
        if List.IsEmpty(List.Intersect({Add_FIMTN[Check], CWBConso[Dummy.Check]})) then Add_FIMTN else    #"Replaced both Non-blank"
    ),
    Sorted = fx("SortNIndex")(Checked),
    //
    CN_FIMTN_Review = Get("Header_S_Dynamic_FIMTN"),
    CN_DLD_Conso_CWB = CWB("DLD_Conso", "CN"),
    CN = List.Distinct(CN_FIMTN_Review & CN_DLD_Conso_CWB & Table.ColumnNames(Sorted)),
    Reordered = try Table.ReorderColumns(Sorted, CN, MissingField.Ignore) otherwise Sorted
in
    Reordered
