// let LoadCode = Load("ForReview_wBOCOM","Code"), EVA = Expression.Evaluate(LoadCode ,#shared) in EVA
let
    _input = "wBOCOM",
    _PK = "Check",
    Name = "m" & Text.AfterDelimiter(_input, "w"),
    HD = fx("ForReviewHD")(_input),
    Sourceold = Load("DLD_Filtered"),
    Source = Table.SelectRows(CWB("Filtered_Add"), each [IS_FIMTN] <> "Y" and  [IS_FIMTN] <> "【Y】" ),
    Prep =
        if not List.Contains(Table.ColumnNames(Source), "BISL_ROLE") then
            Table.AddColumn(
                Source,
                "BISL_ROLE",
                each
                    if Text.Contains([联席全球协调人], "BOCOM") then
                        "JGC"
                    else if Text.Contains([牵头经办人], "BOCOM") then
                        "JBR"
                    else if Text.Contains([联席全球协调人], "【") then
                        "【】"
                    else
                        "--"
            )
        else
            Source,
    DLD_QRC = CWB("DLD_QRC_Income24"),
    MergedQ = try fx("Merge")(Prep, DLD_QRC, "ISIN", "ISIN", {""}, {""}) otherwise Prep,
    Custom1 = Table.RenameColumns(MergedQ, {{"序号", "台账序号"}}, MissingField.Ignore),
    Filtered = Table.SelectRows(
        Custom1,
        each if Table.HasColumns(Custom1, "IS_JGC") then [BISL_ROLE] <> "--" or [IS_JGC] <> "" else
            [BISL_ROLE] <> "--"
    ),
    ACol0 = Table.AddColumn(
        Filtered, "COVERAGE", each if not List.Contains({null, "", "--"}, [团队]) then [团队] & "组" else "", type text
    ),
    #">>ExpandedList" = List.RemoveItems(Table.ColumnNames(Custom1), Table.ColumnNames(Prep)),
    SCol = fx("Data")(Table.SelectColumns(ACol0, HD, MissingField.UseNull)),
    Replaced =
        if Table.HasColumns(Filtered, "IS_JGC") then
            Table.ReplaceValue(
                SCol,
                each [BISL_ROLE],
                each
                    if [IS_JGC] <> "" and [BISL_ROLE] = "【】" then
                        [IS_JGC]
                    else if [IS_JGC] <> "" and [BISL_ROLE] <> [IS_JGC] then
                        "【" & [BISL_ROLE] & "】" & "#(lf)" & "【" & [IS_JGC] & "】"
                    else
                        [BISL_ROLE],
                Replacer.ReplaceValue,
                {"BISL_ROLE"}
            )
        else
            SCol,
    BlankToNil =
        try
            Table.ReplaceValue(
                Replaced, "", "nil", Replacer.ReplaceValue, List.RemoveItems(#">>ExpandedList", {"新规角色", "COVERAGE"})
            ) otherwise Replaced,
    BlankToInput = Table.ReplaceValue(
        BlankToNil, "", "【】", Replacer.ReplaceValue, {"台账序号", "EXE", "PROJECT_CODE", "新规角色", "COVERAGE"}
    ),
    BlankToInput1 = Table.ReplaceValue(
        BlankToInput, "", each if [BISL_ROLE] <> "JBR" then "【】" else "", Replacer.ReplaceValue, {
            "IS_LEAD",
            "IS_B&D"
        }
    ),
    BlankToInput2 = Table.ReplaceValue(BlankToInput1, "", "--", Replacer.ReplaceValue, {"是否合名"}),
    SelfJoined = fx("SelfJoin_R")(BlankToInput2, CWB("ForReview_" & _input), {_PK}),
    Sorted = fx("SortNIndex")(SelfJoined)
in
    Sorted
