// let LoadCode = Load("ForReview_wCredit","Code"), EVA = Expression.Evaluate(LoadCode ,#shared) in EVA
let
    Source = try CWB("DLD_Conso") otherwise Load("DLD_Conso"),
    FromForReviewIssuer = Table.SelectColumns(
        Table.SelectRows(Load("ForReview_Issuer"), each Text.Contains([信用主体], "【")), {"信用主体"}
    ),
    Combined = fx("Combine")({Source, FromForReviewIssuer}, {"信用主体"}),
    TFCol = Table.TransformColumns(
        Combined,
        {
            {
                "信用主体",
                each
                    if Text.Contains(_, "【") then
                        Text.BetweenDelimiters(Text.BeforeDelimiter(_, "【", {0, RelativePosition.FromEnd}), "【", "】")
                    else
                        _
            }
        }
    ),
    Replaced = Table.ReplaceValue(TFCol, null, each [信用主体], Replacer.ReplaceValue, {"债券简称"}),
    MergedQ = Table.NestedJoin(
        Replaced, {"信用主体"}, fx("Excel")("DEALSMASTER_mCredit"), {"信用主体"}, "Dummy" & "Data", JoinKind.LeftAnti
    ),
    Function = Table.AddColumn(MergedQ, "SHORTNAMER", each fx("SNBankCorp")([债券简称])),
    SCol1 = Table.SelectColumns(
        Function, {"信用主体", "SHORTNAMER", "板块", "板块二级", "板块三级", "中资属性", "级别", "地区", "城市", "区县"}, MissingField.UseNull
    ),
    BlankToInput = Table.ReplaceValue(SCol1, null, "【】", Replacer.ReplaceValue,{"板块","中资属性"})
in
    BlankToInput
