// let LoadCode = Load("DLD_Add","Code"), EVA = Expression.Evaluate(LoadCode ,#shared) in EVA
let
    Source = try CWB("DLD_Conso") otherwise Load("DLD_Conso"),
    CN_Source = List.Buffer(Table.ColumnNames(Source)),
    #"Removed and Renamed" = Table.RenameColumns(
        Table.RemoveColumns(Source, {"计息基准", "IPT_DMI", "FPG_DMI"}, MissingField.Ignore),
        {{"IPG", "IPT_DMI"}, {"FPG", "FPG_DMI"}, {"浮息定价", "计息基准"}},
        MissingField.Ignore
    ),
    //
    Clean_TBC =
        if List.Contains(CN_Source, "IS_FIMTN") then
            fx("Replace")(#"Removed and Renamed", {"IS_FIMTN"}, "RTBC")
        else
            #"Removed and Renamed",
    Clean_Structure = fx("Replace")(Table.DuplicateColumn(Clean_TBC, "发行架构", "结构"), {"结构"}, "结构"),
    Add_Tap = Table.AddColumn(
        Clean_Structure,
        "增发判定",
        each if [发行方式] = "增发" then "增发" else if [起息日期] = null then "" else if [定价日] > [起息日期] then "增发" else "",
        type text
    ),
    Added = List.Accumulate(
        {
            "SBLC",
            "Exchange",
            "BankBranch",
            "BBGFX",
            "Stats",
            "BCM",
            "FRNString",
            "CpnString",
            "IPGString",
            "FPGString",
            "SpdTighten",
            "PtTighten",
            "Tenor",
            "ESGMore"
        },
        Add_Tap,
        (x, y) => fx("Add" & y)(x)
    ),
    WeeklyAddTapSwith = CWB("WeeklyAddTap"),
    Appended = if WeeklyAddTapSwith = "OFF" then Added else fx("Combine")({Added, Load("wAddTapPQ")}, {null}, {0}),
    #"EntityRtg TBC" = Table.ReplaceValue(
        Appended,
        each [主体评级],
        each
            if
                [主体评级] = "--/--/--" and [发行评级] <> "--/--/--"
              
            then
                "【" & [发行评级] & "】"
            else
                [主体评级],
        Replacer.ReplaceValue,
        {"主体评级"}
    ),
    //
    Checked = fx("CheckNDupe")(#"EntityRtg TBC"),
    Sorted = fx("SortNIndex")(Checked)
in
    Sorted
