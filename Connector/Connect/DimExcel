let
    SourceFD = Table.Buffer(Load("DimFolder_Input", "CSV")),
    RCol = Table.RemoveColumns(SourceFD, if Load("GPS") <> "Remote" then {"Column2"} else {"Column3"}),
    #"Promoted Headers" = Table.PromoteHeaders(RCol, [PromoteAllScalars = true]),
    #"Removed Top Rows" = Table.Skip(#"Promoted Headers", 1),
    Monday =   Date.ToText(List.Buffer(Load("Controller")[Monday]){0},"yyyyMMdd"),
    #"Replaced Value" = Table.ReplaceValue(
        #"Removed Top Rows", "YYYYMMDD", Monday, Replacer.ReplaceText, {"Has_NumInPath"}
    ),
    #"Added Custom" = Table.AddColumn(
        #"Replaced Value",
        "Path",
        each
            if List.Contains({null, "", "--"}, [Has_NumInPath]) then
                [Folder_Path]
            else
                [Folder_Path] & "\" & Text.Replace([Has_NumInPath], "/", "")
    ),
    #"Filtered Rows" = Table.SelectRows(#"Added Custom", each ([Folder_Path] <> "")),
    #"Invoked Custom Function" = Table.AddColumn(#"Filtered Rows", "Data", each Load("fxFolder")([Path], ".xls")),
    #"Removed Other Columns" = Table.SelectColumns(#"Invoked Custom Function", {"Data", "PK_Folder"}),
    #"Expanded Data" = Table.ExpandTableColumn(
        #"Removed Other Columns",
        "Data",
        List.RemoveItems(
            Table.ColumnNames(#"Removed Other Columns"[Data]{0}), Table.ColumnNames(#"Removed Other Columns")
        )
    ),
    #"Inserted Text Before Delimiter" = Table.AddColumn(
        #"Expanded Data",
        "PK_Excel",
        each
            Text.BeforeDelimiter(
                Text.BeforeDelimiter(Text.BeforeDelimiter([Name], ".", {0, RelativePosition.FromEnd}), "-"), "_"
            ),
        type text
    ),
    #"Merged Columns" = Table.Buffer(
        Table.CombineColumns(
            #"Inserted Text Before Delimiter",
            {"Folder Path", "Name"},
            Combiner.CombineTextByDelimiter("", QuoteStyle.None),
            "File Path"
        )
    ),
    #"Merged Queries" = Table.NestedJoin(
        Load("DimExcel_Input", "CSV"),
        {"PK_Folder", "PK_Excel"},
        #"Merged Columns",
        {"PK_Folder", "PK_Excel"},
        "Data",
        JoinKind.LeftOuter
    ),
    #"Expanded Data1" = Table.ExpandTableColumn(
        #"Merged Queries",
        "Data",
        List.RemoveItems(Table.ColumnNames(#"Merged Queries"[Data]{0}), Table.ColumnNames(#"Merged Queries"))
    ),
    TF = Table.TransformColumns(#"Expanded Data1", {{"Sheet_Key", Text.Upper, type text}}),
    TF2 = Table.TransformColumnTypes(TF, {{"Table_SkipNPromote", Int64.Type}}),
    A1 = Table.AddColumn(
        TF2, "Key", each if [Sheet_Key] <> "" and [Sheet_Key] <> "*SOLE*" then [Excel_Key] & "_" & [Sheet_Key] else [Excel_Key]
    ),
     A2 = Table.AddColumn(
        A1, "fxTF_Key", each if [fxTF] <> "" then [fxTF]&"_"&[Key] else "",type text
    ),
        A3 = Table.AddColumn(
        A2, "RN_Key", each if [RN] <> "" then [RN]&"_"&[Key] else "",type text
    ),
    #"Removed Other Columns1" = Table.SelectColumns(
        A3, {"Content", "File Path", "Key", "Excel_Key", "Sheet_Key", "Table_SkipNPromote", "Table_FindHD", "Table_FindAnchor","fxTF_Key","RN_Key"}
    ),
    #"Filtered Rows1" = Table.SelectRows(#"Removed Other Columns1", each not List.Contains({"",null}, [File Path] ))
in
    #"Filtered Rows1"