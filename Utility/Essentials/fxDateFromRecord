(_input as list, optional SCollist as list) =>
    //  fx("DateFromRecord")(fx("DateRange")({"近5年", "当前"}),{"每周第一天"}),
    let
        DateList = _input,
        //
        Output = #table(
            type table [
                A_Date = date,
                Year.Num = Int64.Type,
                MM = Int64.Type,
                WeekNum = Int64.Type,
                WeekStartDate = date,
                QEndDate = date,
                YYYY = text,
                YYYY.至今 = text,
                YYQQ = text,
                YYMMDD = text,
                DDMMMYYYY = text,
                DayName = text,
                DayName.CN = text,
                DayShortName = text,
                MonthName.CN = text,
                年M月 = text,
                年月 = text,
                年 = text,
                季 = text,
                月 = text,
                周 = text,
                每周第一天 = text
            ],
            List.Transform(
                DateList,
                each
                    {
                        _,
                        Date.Year(_),
                        Date.Month(_),
                        Date.WeekOfYear(_, Day.Monday),
                        Date.StartOfWeek(_, Day.Monday),
                        Date.EndOfQuarter(_),
                        Date.ToText(_, "yyyy"),
                        if Date.Month(_) = 1 then
                            Date.ToText(Date.AddYears(_, -1), "yyyy")
                        else
                            Date.ToText(_, "yyyy"),
                        Date.ToText(_, "yy") & "Q" & Number.ToText(Date.QuarterOfYear(_), "0"),
                        Date.ToText(_, "yyMMdd"),
                        Date.ToText(_, "dd MMM yyyy"),
                        Date.DayOfWeekName(_),
                        Date.DayOfWeekName(_, "zh-CN"),
                        Date.ToText(_, "ddd"),
                        Date.MonthName(_, "zh-CN"),
                        Date.ToText(_, "yy年") & Number.ToText(Date.Month(_), "0月"),
                        Date.ToText(_, "yy年") & Number.ToText(Date.Month(_), "00月"),
                        Date.ToText(_, "yy年"),
                        Date.ToText(_, "yy年") & Number.ToText(Date.QuarterOfYear(_), "0") & "季度",
                        Number.ToText(Date.Month(_), "00月"),
                        Number.ToText(Date.WeekOfYear(_, Day.Monday), "00周"),
                        Date.ToText(Date.StartOfWeek(_, Day.Monday), "yyyy年MM月dd日起一周")
                    }
            )
        ),
        SCol = if SCollist = null then Output else Table.SelectColumns(Output, SCollist, MissingField.UseNull)
        // Keys =  if Table.HasColumns(SCol, "A_Date") then Table.AddKey(SCol, {"A_Date"},true) else SCol
    in
        SCol
